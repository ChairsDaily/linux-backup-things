//Custom Weapons Script by Keettu and gy_be
//This will load custom directory weapons based on gamemode and team (where it applies in team related modes, else return to defaults in FFA modes)
//Version 3.0: Now includes cla and rvsf huddigits, along with team colored frag messages in console.

baseweaponmodeldirs = "knife pistol carbine shotgun subgun sniper assault cpistol grenade akimbo"

onSpawnForCustomWeaponsScript = [
    weaponmodeldirsuffix = ""

    if (curmodeattr team) [
        if (= (curteam) 0) [
            weaponmodeldirsuffix = _cla
        ]

        if (= (curteam) 1) [
            weaponmodeldirsuffix = _rvsf
        ]
    ]
	font huddigits (concatword "packages/misc/huddigits" $weaponmodeldirsuffix ".png") 40 64
	fontskip 47
	fontchar 476 64  24  64        // /
	fontchar 4   64  40  64        // 0
	fontchar 52  64  40  64        // 1
	fontchar 100 64  40  64        // 2
	fontchar 148 64  40  64        // 3
	fontchar 196 64  40  64        // 4
	fontchar 244 64  40  64        // 5
	fontchar 292 64  40  64        // 6
	fontchar 340 64  40  64        // 7
	fontchar 388 64  40  64        // 8
	fontchar 436 64  40  64        // 9
    loop weaponindex (listlen $baseweaponmodeldirs) [
        weaponmodeldir = (at $baseweaponmodeldirs $weaponindex)

        if (! (strcmp $weaponmodeldir cpistol)) [
            (concatword modmdlweap $weaponindex) = (concatword $weaponmodeldir $weaponmodeldirsuffix)
            (concatword modmdlvwep $weaponindex) = (concatword $weaponmodeldir $weaponmodeldirsuffix)
        ]
    ]
]

checkinit onSpawn onSpawnForCustomWeaponsScript

//Team Color messages by gy_be

fragmessage1 = "busted" // Pistol
fragmessage2 = "picked off" // Carbine
fragmessage3 = "peppered" // SG
fragmessage4 = "sprayed" // SMG
fragmessage5 = "punctured" // SR
fragmessage6 = "shredded" // AR
fragmessage9 = "busted" // Akimbo

gibmessage0 = "slashed" // Knife
gibmessage3 = "splattered" // Shotgun splat
gibmessage5 = "headshot" // HS
gibmessage8 = "gibbed" // Nade

setkillmessagesuffix = [
    loop weaponindex 10 [
        looplist "frag gib" killtype [
            killmessage = (getalias (concatword $killtype message $weaponindex))

            if (! (strcmp (substr (c 0) 0 1) (substr $killmessage 0 1))) [
                killmessage = (concatword (c 2) $killmessage)
            ]

            (concatword $killtype message) $weaponindex (concatword $killmessage $arg1)
        ]
    ]
]

playerteamcolor = [
    if (curmodeattr team) [
        if (|| (= (player $arg1 team) 0) (= (player $arg1 team) 2)) [
            result (c 3)
        ] [
            if (!= (player $arg1 team) 4) [
                result (c 1)
            ] [
                result (c 2)
            ]
        ]
    ] [
        result (c 2)
    ]
]

updatekillmessageonkill = [
    if (= $arg2 (player1 cn)) [
        setkillmessagesuffix (playerteamcolor $arg1)
    ] [
        setkillmessagesuffix (playerteamcolor $arg2)
    ]
]

checkinit onKill updatekillmessageonkill

//Custom Scope Crosshair For CLA and RVSF by gy_be

onSpawnForPerTeamScopeCrosshair = [
    selectedscopecrosshair = "green_dot"

    if (curmodeattr team) [
        if (= (curteam) 0) [
            selectedscopecrosshair = "red_dot"
        ]

        if (= (curteam) 1) [
            selectedscopecrosshair = "cyan_dot"
        ]
    ]

    loadcrosshair (concatword $selectedscopecrosshair .png) scope
]

checkinit onSpawn onSpawnForPerTeamScopeCrosshair